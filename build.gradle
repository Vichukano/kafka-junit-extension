import com.vanniktech.maven.publish.SonatypeHost
import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

plugins {
    id 'java'
    id 'signing'
    id "com.vanniktech.maven.publish" version "0.31.0-rc2"
}

def ver = "1.2.1"
def projectName = "kafka-junit-extension"
group = "io.github.vichukano"
version = ver

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    def lombokVersion = '1.18.36'
    def springKafkaVersion = "3.3.4"
    def junitVersion = "5.9.1"
    compileOnly "org.junit.jupiter:junit-jupiter-api:${junitVersion}"
    compileOnly "org.apache.kafka:kafka-clients:3.8.1"
    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.slf4j:slf4j-api:2.0.17"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    testImplementation "org.springframework.kafka:spring-kafka-test:${springKafkaVersion}"
    testImplementation "org.springframework.kafka:spring-kafka:${springKafkaVersion}"
    testImplementation "org.assertj:assertj-core:3.23.1"
    testImplementation "org.junit.jupiter:junit-jupiter-engine:${junitVersion}"
    testImplementation "ch.qos.logback:logback-classic:1.4.5"
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
}

tasks.withType(Javadoc).configureEach {
    failOnError false
}

signing {
    useGpgCmd()
    sign publishing.publications
}

mavenPublishing {
    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL)
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
    coordinates(group, projectName, ver)
    pom {
        packaging = "jar"
        name = projectName
        url = "https://github.com/Vichukano/kafka-junit-extension"
        description = "Library for testing"

        licenses {
            license {
                name = "The Apache License, Version 2.0"
                url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
            }
        }

        scm {
            connection = "scm:https://github.com/Vichukano/kafka-junit-extension.git"
            developerConnection = "scm:git@github.com:Vichukano/kafka-junit-extension.git"
            url = "https://github.com/Vichukano/kafka-junit-extension"
        }

        developers {
            developer {
                id = "Vichukano"
                name = "Victor Chukanov"
                email = "vichukano@gmail.com"
            }
        }
    }
}
